AppType=StandardJava
Build1=Default,b4j.webapi,hu2_acceptall
File1=config.example
File2=index.html
File3=main.html
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
Group=Default Group
Library1=byteconverter
Library2=javaobject
Library3=jcore
Library4=jserver
Library5=json
Library6=keyvaluestore
Module1=ApiHandler
Module10=WebUtils
Module2=CorsFilter
Module3=DataController
Module4=HelpHandler
Module5=HttpsFilter
Module6=IndexController
Module7=MinimaList
Module8=Utility
Module9=WebHandler
NumberOfFiles=3
NumberOfLibraries=6
NumberOfModules=10
Version=9.8
@EndOfDesignText@
' Name: 			Web API Template
' Description: 		Non-UI application (console / server application)
' Version: 			2.02.1

' Objects folder: 	file:///C:/B4X/Development/Web%20API%20Server%202.02/Objects (Bring to front)
' Objects folder: 	ide://run?file=%WINDIR%\SysWOW64\explorer.exe&Args=%PROJECT%\Objects

' Main module: 		ide://goto?Module=Main&Sub=ConfigureServer
' DataController: 	ide://goto?Module=DataController&Sub=GetData
' ApiHandler: 		ide://goto?Module=ApiHandler&Sub=ProcessRequest
' HelpHandler: 		ide://goto?Module=HelpHandler&Sub=GenerateResponseScript

' Click to publish: ide://run?file=%JAVABIN%\jar.exe&WorkingDirectory=../Objects&Args=-cMf&Args=Publish.zip&Args=www&Args=*.jar&Args=*.ini&Args=*.db&Args=help.html&Args=help.js

#Region Project Attributes 
	#CommandLineArgs:
	#MergeLibraries: True
#End Region

Sub Process_Globals
	Public srvr As Server
	Public ROOT_URL As String
	Public ROOT_PATH As String
	Public API_PATH As String
	Public API_NAME As String
	Public API_VERSIONING As String
	Public SERVER_PORT As Int = 8080
	Public SSL_PORT As Int
	Public Config As Map
	Public Element As Element
	Public Controllers As List
	Public SimpleResponse As Boolean
	Public SimpleResponseFormat As String
	Public KVS_ENABLED As Boolean
	Public KVS As KeyValueStore
	Public Minima As MinimaList
	Public const VERSION As String = "2.02.1"
	Type ApiElement (Name As String, Versioning As Boolean)
	Type WebElement (Root As String, Path As String)
	Type Element (Web As WebElement, Api As ApiElement, Elements As List, PathIndex As Int, ApiVersionIndex As Int, ApiControllerIndex As Int, WebControllerIndex As Int)
End Sub

' <link>Click to browse|http://127.0.0.1:8080/web</link>
Sub AppStart (Args() As String)
	srvr.Initialize("")		' Create the server
	ReadConfig				' Read environment settings
	Minima.Initialize		' Initialize MinimaList
	ConfigureServer			' Configure the server
	srvr.Start				' Start the server
	StartMessageLoop
End Sub

Sub ReadConfig
	If Not(File.Exists(File.DirApp, "config.ini")) Then
		File.Copy(File.DirAssets, "config.example", File.DirApp, "config.ini")
	End If
	Config = Utility.ReadMapFile(File.DirApp, "config.ini")
	Config.Put("VERSION", VERSION)
End Sub

Private Sub ConfigureServer
	ConfigurePaths								' App URL / Paths
	ConfigureElements							' Pretty URL structures / hierarchy
	ConfigureHandlers							' Server Handlers
	ConfigureControllers						' App Controllers
	ConfigureResponse							' JSON Response Format
	ConfigureWelcomeText						' Show information in Logs when server starts
	
    #Region Advanced
	'ConfigurePort								' Server Port
	'ConfigureSSL								' SSL Port
	ConfigureCORS								' CrossOriginFilter
	'ConfigureStaticFiles 						' Configure Static Files Folder and Browsing Permission
	'ConfigureTimezone							' Adjust timezone if server located in different timezone
	'ConfigureSessions							' Enable Sessions
	'ConfigureCookies							' Enable Cookies
	'ConfigureDatabase							' Enable Database
	ConfigureKeyValueStore						' Enable KeyValueStore
	'ConfigureBasicAuth							' Enable Basic Authentication
	'ConfigureJWTAuth							' Enable JSON Web Token Authentication
	#End Region
End Sub

' First thing to configure
' This setting is determined by the values in Config.ini (inside Objects folder).
' For starting, just let the default values.
' You can edit the Config.ini file using any Text Editor.
Public Sub ConfigurePaths
	ROOT_URL = Config.Get("ROOT_URL")
	If SERVER_PORT <> 80 Then
		ROOT_URL = ROOT_URL & ":" & SERVER_PORT
	End If
	If SSL_PORT <> 0 Then
		' Update ROOT_URL
		ROOT_URL = ROOT_URL.Replace("http:", "https:")
		ROOT_URL = ROOT_URL.Replace($":${SERVER_PORT}"$, $":${SSL_PORT}"$)
	End If
	Config.Put("ROOT_URL", ROOT_URL)
	
	ROOT_PATH = Config.Get("ROOT_PATH")
	If ROOT_PATH = "" Then ROOT_PATH = "/"
	If ROOT_PATH <> "/" Then
		If ROOT_PATH.StartsWith("/") = False Then ROOT_PATH = "/" & ROOT_PATH
		If ROOT_PATH.EndsWith("/") = False Then ROOT_PATH = ROOT_PATH & "/"
	End If
	Config.Put("ROOT_PATH", ROOT_PATH)
	
	API_NAME = Config.Get("API_NAME")
	If API_NAME.Contains("/") Then
		Log($"API name cannot contains forward slash symbol!"$)
		Log($"Application is terminated."$)
		ExitApplication
		Return
	End If
	API_PATH = IIf(API_NAME.Length > 0, API_NAME & "/", "")
	API_VERSIONING = Config.Get("API_VERSIONING").As(String).ToUpperCase
End Sub

' Configure this after ConfigurePaths
' This setting depends automatically by ConfigurePaths above.
' Nothing is needed to be configured here.
Public Sub ConfigureElements
	Element.Initialize
	Element.Elements.Initialize
	
	Element.Web.Root = "/" ' just a placeholder
	Element.Elements.Add(Element.Web.Root)
	
	Element.Web.Path = ROOT_PATH
	If Element.Web.Path.EqualsIgnoreCase("/") = False Then
		Element.Elements.Add(Element.Web.Path)
		Element.PathIndex = 1
	End If
	
	Element.Api.Name = API_NAME
	If Element.Api.Name.EqualsIgnoreCase("") = False Then
		Element.Elements.Add(Element.Api.Name)
	End If
	
	If API_VERSIONING = "TRUE" Then
		Element.Api.Versioning = True
		Element.Elements.Add("version") ' just a placeholder
	Else
		Element.Api.Versioning = False
	End If
	
	Element.ApiVersionIndex = Element.Elements.Size - 1
	Element.ApiControllerIndex = Element.Elements.Size
	Element.WebControllerIndex = Element.Elements.Size - IIf(Element.Api.Name.EqualsIgnoreCase(""), 0, 1) - IIf(Element.Api.Versioning, 1, 0) ' or just Element.PathIndex + 1
	
    #Region debug
	'#If debug
	'Dim i As Int
	'For Each item In Element.Elements
	'	Log($"${i} - ${item}"$)
	'	i = i + 1
	'Next
	'Log("PathIndex=" & Element.PathIndex)
	'Log("WebControllerIndex=" & Element.WebControllerIndex)
	'Log("ApiVersionIndex=" & Element.ApiVersionIndex)
	'Log("ApiControllerIndex=" & Element.ApiControllerIndex)
	'#End If	
    #End Region
End Sub

' Configure this after ConfigureElements
' By default, ApiHandler is the only required handler.
' If WebHandler is enabled (uncommented), then we can use it to route non-API URL to show web front-end such as home page.
' If HelpHandler is enabled (by default), API documentation is available for debugging the API without client app or Postman.
' If HelpHandler is disabled (commented) then API documentation is not available for debugging. This handler is not required in release mode.
Public Sub ConfigureHandlers
	' =======================================================================
	' Web route (optional)
	' =======================================================================
	' Note: When only WebHandler is enabled, suitable for creating a website
	srvr.AddHandler(ROOT_PATH & "*", "WebHandler", False) 						' Add Web handler (optional)
	
	If API_NAME.EqualsIgnoreCase("") Then
		' ===================================================================
		' API route - /web/ (root path is used for all APIs)
		' ===================================================================
		' Note: You can either enable WebHandler or ApiHandler, not both     
		srvr.AddHandler(ROOT_PATH & "*", "ApiHandler", False) 					' Add API handler (WebHandler disabled)
	Else									
		' ===================================================================
		' API route - /web/api/ (recommended)
		' ===================================================================
		' Note: You can enable both WebHandler and ApiHandler
		srvr.AddHandler(ROOT_PATH & API_PATH & "*", "ApiHandler", False) 		' Add API handler
	End If
	
	' =======================================================================
	' Help route
	' =======================================================================
	srvr.AddHandler(ROOT_PATH & "help", "HelpHandler", False) 					' Add Help page (optional)
	
	' =======================================================================
	' Web Sockets route
	' =======================================================================
	'srvr.AddWebSocket("/time", "WSTime")										' Add Web socket (optional)
End Sub

' Configure this after ConfigureHandlers
' If HelpHandler is used, this configuration includes which controller class to display on the API documentation page. 
' By default, only DataController is added. 
' When you have more controllers, you can add them here.
' You can be hide some APIs using #Hide keyword
Public Sub ConfigureControllers
	Controllers.Initialize
	Controllers.Add("DataController")
End Sub

' Configure JSON response format
' When enabled, the default format is the original Map convert to JSON string
' When disabled (commented), standard API format with be returned
' Standard JSON object has 'm', 'e', 's', 'r', 'a' keys where the response (r) is a list
Public Sub ConfigureResponse
	' =======================================================================
	' Check example: ide://goto?Module=Utility&Sub=ReturnSimpleHttpResponse
	' =======================================================================
	SimpleResponse = True
	SimpleResponseFormat = "Map"
End Sub

' Display some information in Logs (debug) or terminal (release)
' You may edit your own text
Public Sub ConfigureWelcomeText
	Log($"Web API server (version = ${VERSION}) is running on port ${srvr.Port}${IIf(srvr.SslPort > 0, $" (redirecting to port ${srvr.SslPort})"$, "")}"$)
	Log($"Open the following URL from your web browser (if help is enabled)"$)
	Log(ROOT_URL & ROOT_PATH & "help")
End Sub

#Region Additional Configurations
' This configuration is automatically read from the values in Config.ini
' By default, server port is set to 8080
Public Sub ConfigurePort
	SERVER_PORT = Config.Get("ServerPort")
	If SERVER_PORT = 0 Then
		Log($"Server Port is not set!"$)
		Log($"Application is terminated."$)
		ExitApplication
	End If
	srvr.Port = SERVER_PORT
End Sub

' This configuration is automatically read from the values in Config.ini
' By default, SSL port is disabled
Public Sub ConfigureSSL
	SSL_PORT = Config.Get("SSLPort") : If IsNumber(SSL_PORT) = False Then SSL_PORT = 0
	If SSL_PORT = 0 Then Return
	
	Dim KeyStoreDir As String = Config.Get("SSL_KEYSTORE_DIR")
	Dim KeyStoreFile As String = Config.Get("SSL_KEYSTORE_FILE")
	Dim KeyStorePassword As String = Config.Get("SSL_KEYSTORE_PASSWORD")
	
	Dim ssl As SslConfiguration
	ssl.Initialize
	ssl.SetKeyStorePath(KeyStoreDir, KeyStoreFile)
	ssl.KeyStorePassword = KeyStorePassword
	'ssl.KeyManagerPassword = ""
	srvr.SetSslConfiguration(ssl, SSL_PORT)
	
	'add filter to redirect all traffic from http to https (optional)
	srvr.AddFilter("/*", "HttpsFilter", False)
End Sub

' This configuration is use for Cross Origin in JavaScript call
' By default, no control is implemented
Public Sub ConfigureCORS
	' =========================================================
	' Note: If you have enabled JWT then you may not need this
	' =========================================================
	#Region Example
	' allowedOrigins = "*" or "http://google.com"
	' allowedMethods = "*" or "GET,POST,HEAD"
	' allowedHeaders = "*" or "X-Requested-With,Content-Type,Accept,Origin"
	' Eg. ConfigureCORS(ROOT_PATH & "account/*", "*", "", "")
	' Reference: https://www.b4x.com/android/forum/threads/jetty-cross-origin-filter-to-be-added-to-jserver-library.85641/
	#End Region
	Dim Paths As List
	Paths.Initialize
	
	'Paths.Add(CreateMap("path": "*", "origins": "*", "methods": "POST,PUT,DELETE", "headers": "*")) 		' All origins access (* methods not working)
	Paths.Add(CreateMap("path": ROOT_PATH & API_PATH & "v2/data/*", "origins": "http://localhost, http://127.0.0.1:3000", "methods": "POST,PUT,DELETE", "headers": "*")) ' vue.js prod/dev app
	
	For Each Item As Map In Paths
		Dim cors As CorsFilter
		cors.Initialize(Item.Get("path"), _
		CreateMap( _
		"allowedOrigins": Item.Get("origins"), _
		"allowedMethods": Item.Get("methods"), _
		"allowedHeaders": Item.Get("headers"), _
		"allowCredentials": "true", _
		"preflightMaxAge": 1800, _
		"chainPreflight": "false"))
		cors.AddToServer(srvr)
	Next
End Sub

' Set the public accessible folder and disallow browsing
Public Sub ConfigureStaticFiles
	' ========================================================
	' Share the asset files such as css, js, images and fonts
	' The default values will be applied if not set
	' ========================================================
	srvr.StaticFilesFolder = File.Combine(File.DirApp, "www")
	srvr.SetStaticFilesOptions(CreateMap("dirAllowed": False))
End Sub

' Use this to adjust server time offset (may not needed)
'
' Add the following line to Process_Globals
' <code>Public TIMEZONE As Int</code>
Public Sub ConfigureTimezone
	' ============================================================
	' Uncomment the following code
	' ============================================================
	'TIMEZONE = Config.Get("TimeZone") : If IsNumber(TIMEZONE) = False Then TIMEZONE = 0
End Sub

' Use Sessions
'
' Add the following lines to Process_Globals
' <code> Public const PREFIX As String = "WebAPI_v2_"
' Public SESSIONS_ENABLED As Boolean</code>
Public Sub ConfigureSessions
	' ====================================================================
	' Make sure PREFIX constant is set if you have more than one instance
	' Uncomment the following code
	' ====================================================================
	'SESSIONS_ENABLED = True
End Sub

' Use Cookies
'
' Add the following lines to Process_Globals
' <code> Public const PREFIX As String = "WebAPI_v2_"
' Public COOKIES_ENABLED As Boolean
' Public COOKIES_EXPIRATION As Long</code>
Public Sub ConfigureCookies
	' ====================================================================
	' Make sure PREFIX constant is set if you have more than one instance
	' Uncomment the following code
	' ====================================================================
	'COOKIES_ENABLED = True
	'COOKIES_EXPIRATION = 90 * 24 * 60 * 60 			' in seconds = 90 days
End Sub

' Initialize a database connector object
'
' Add <link>DataConnector|https://www.b4x.com/android/forum/threads/web-api-template-2.143310/#post-908109</link> class to the Modules tab
' Add the following line to Process_Globals
' <code> Public DB As DataConnector</code>
Public Sub ConfigureDatabase
	' ============================================================
	' You need to select jSQL from the Libraries Manager
	' Add relevant JDBC driver using #AdditionalJar
	' Uncomment the following code
	' ============================================================
	'DB.Initialize(Config)
End Sub

' Initialize KeyValueStore
Public Sub ConfigureKeyValueStore
	' ============================================================
	' Persistent Storage for MinimaList
	' You need to select KeyValueStore from the Libraries Manager
	' ============================================================
	KVS.Initialize(File.DirApp, "kvs.dat")
	' Load values
	If Minima.IsInitialized Then
		Minima.List = KVS.GetDefault("List", Minima.List)
	End If
	KVS_ENABLED = True
	Log("KeyValueStore is enabled")
End Sub

' Use Basic Authentication
'
' Add <link>BasicAuthFilter|https://www.b4x.com/android/forum/threads/web-api-template-2.143310/#post-908109</link> and <link>MiniORM|https://www.b4x.com/android/forum/threads/web-api-template-2.143310/#post-908109</link> classes to the Modules tab
' Add the following lines to Process_Globals
' <code> Public DB As DataConnector
' Public AUTHENTICATION_TYPE As String
' Public AUTH As AUTH</code>
Public Sub ConfigureBasicAuth
	' ==============================
	' Uncomment the following code
	' ==============================
	'AUTHENTICATION_TYPE = "BASIC AUTHENTICATION"
	'
	'Dim Paths As List
	'Paths.Initialize
	'Paths.Add(ROOT_PATH & "dashboard")
	'
	'For Each path In Paths
	'	'Log(path)
	'	srvr.AddFilter(path, "BasicAuthFilter", False)
	'Next
End Sub

' Use JSON Web Token Authentication
'
' Add <link>JSONWebToken|https://www.b4x.com/android/forum/threads/web-api-template-2.143310/#post-908109</link> and <link>JWTAuthFilter|https://www.b4x.com/android/forum/threads/web-api-template-2.143310/#post-908109</link> classes to the Modules tab
' Add the following line to Process_Globals
' <code> Public DB As DataConnector
' Public AUTHENTICATION_TYPE As String
' Public JAT As JSONWebToken
' Public JRT As JSONWebToken
' Public Secret As Secret
' Type Secret (Access_Token As String, Refresh_Token As String)</code>
Public Sub ConfigureJWTAuth
	' ====================================================================
	' Make sure database is created and server has restarted at first run
	' Uncomment the following code
	' ====================================================================
	'If DB.Busy Then
	'	LogColor("JSON Web Token is Not Enabled", -65536)
	'	Return
	'End If
	'
	'AUTHENTICATION_TYPE = "JSON WEB TOKEN AUTHENTICATION"
	'
	'' Enable this only after database is generated
	'' You need to restart the server after first run if database not exist
	'Dim con As SQL = DB.GetConnection
	'Dim res As ResultSet = con.ExecQuery(DB.Queries.Get("SELECT_LAST_TOKENSECRET"))
	'Do While res.NextRow
	'	Secret.Access_Token = res.GetString("access_token_secret")
	'	Secret.Refresh_Token = res.GetString("refresh_token_secret")
	'Loop
	'res.Close
	'DB.CloseDB(con)
	'
	'JAT.Initialize("HMAC256", Secret.Access_Token, False)
	'JAT.Issuer = ROOT_URL
	'
	'JRT.Initialize("HMAC256", Secret.Refresh_Token, False)
	'JRT.Issuer = ROOT_URL
	'
	'Dim Paths As List
	'Paths.Initialize
	'Paths.Add(ROOT_PATH & "help")
	'
	'For Each path In Paths
	'	'Log(path)
	'	srvr.AddFilter(path, "JWTAuthFilter", False)
	'Next
End Sub
#End Region